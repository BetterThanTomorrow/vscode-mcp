(ns vscode-mcp.server
  (:require
   ["fs" :as fs]
   ["net" :as net]
   ["vscode" :as vscode]
   [clojure.string :as string]
   [promesa.core :as p]))

(defn- do-log [{:keys [on-log]} level & args]
  (when on-log
    (apply on-log level args)))

(defn- delete-port-file!+ [options ^js port-file-uri]
  (p/create
   (fn [resolve-fn _reject]
     (if-not port-file-uri
       (resolve-fn true)
       (-> (vscode/workspace.fs.delete port-file-uri #js {:recursive false, :useTrash false})
           (p/then (fn [_]
                     (do-log options :info "[Server] Deleted port file:" (.-fsPath port-file-uri))
                     (resolve-fn true)))
           (p/catch (fn [err]
                      (do-log options :error "[Server] Could not delete port file with VS Code API:" err (.-message err))
                      ;; Probably VS Code API unavailable during shutdown - try Node fs fallback
                      (try
                        (let [fs-path (.-fsPath port-file-uri)]
                          (if (.existsSync fs fs-path)
                            (do
                              (.unlinkSync fs fs-path)
                              (do-log options :info "[Server] Deleted port file with fs fallback:" fs-path)
                              (resolve-fn true))
                            (do
                              (do-log options :debug "[Server] Port file already gone (fs fallback check)")
                              (resolve-fn true))))
                        (catch js/Error fs-err
                          (do-log options :warn "[Server] Could not delete port file with fallback either:" fs-err (.-message fs-err))
                          (resolve-fn true))))))))))

(defn- split-buffer-on-newline [buffer]
  (let [lines (string/split buffer #"\n")]
    (cond
      (empty? lines)
      [[] ""]

      ;; Buffer ends with newline - all segments are complete
      (string/ends-with? buffer "\n")
      [(filter (comp not string/blank?) lines) ""]

      :else
      ;; Last line is incomplete
      [(filter (comp not string/blank?) (butlast lines)) (last lines)])))

(defn- parse-request-json [json-str]
  (try
    (let [request-js (js/JSON.parse json-str)]
      (js->clj request-js :keywordize-keys true))
    (catch js/Error e
      {:error :parse-error :message (.-message e) :json json-str})))

(defn- format-response-json [response]
  (str (js/JSON.stringify (clj->js response)) "\n"))

(defn- create-error-response [id code message]
  {:jsonrpc "2.0" :id id :error {:code code :message message}})

(defn- process-segment [{:keys [on-request] :as options} segment]
  (let [request-json (string/trim segment)]
    (if (string/blank? request-json)
      (do
        (do-log options :debug "[Server] Blank line segment received, ignoring.")
        nil)
      (let [parsed (parse-request-json request-json)]
        (if (:error parsed)
          (do
            (do-log options :error "[Server] Error parsing request JSON segment:" (:message parsed) {:json (:json parsed)})
            (create-error-response nil -32700 "Parse error"))
          (do
            (do-log options :debug "[Server] Processing request for method:" (:method parsed))
            (if on-request
              (on-request parsed)
              (create-error-response (:id parsed) -32601 "No request handler configured"))))))))

(defn- process-segments [options segments]
  (keep (partial process-segment options) segments))

(defn- handle-socket-data! [options buffer-atom data-chunk]
  (let [_ (do-log options :debug "[Server] Socket received chunk:" data-chunk)
        _ (vswap! buffer-atom str data-chunk)
        [segments remainder] (split-buffer-on-newline @buffer-atom)
        _ (do-log options :debug "[Server] Split segments:" (pr-str segments) "Remainder:" (pr-str remainder))
        _ (vreset! buffer-atom remainder)
        responses (process-segments options segments)
        _ (do-log options :debug "[Server] Generated responses:" (pr-str responses))]
    responses))

(defn- socket-peer-label [^js socket]
  (try
    (let [remote-address (.-remoteAddress socket)
          remote-port (.-remotePort socket)]
      (when (and remote-address remote-port)
        (str remote-address ":" remote-port)))
    (catch :default _ nil)))

(defn- setup-socket-handlers! [{:keys [active-sockets socket-id-counter] :as options} ^js socket]
  (let [socket-id (swap! socket-id-counter inc)
        peer-label (or (socket-peer-label socket) "unknown")]
    (.setEncoding socket "utf8")
    (swap! active-sockets conj socket)
    (do-log options :info "[Server] Socket connected:" socket-id peer-label)
    (.on socket "close"
         (fn []
           (do-log options :info "[Server] Socket closed:" socket-id peer-label)
           (swap! active-sockets disj socket)))

    (let [buffer (volatile! "")]
      (.on socket "data"
           (fn [data-chunk]
             (let [responses (handle-socket-data! options buffer data-chunk)]
               (doseq [response responses]
                 (when response
                   (if (p/promise? response)
                     (-> response
                         (p/then (fn [resolved-response]
                                   (do-log options :debug "[Server] Sending resolved response:" (pr-str resolved-response))
                                   (.write socket (format-response-json resolved-response))))
                         (p/catch (fn [err]
                                    (do-log options :error "[Server] Error resolving response:" err)
                                    (let [error-response (create-error-response nil -32603 (str "Internal error: " err))]
                                      (.write socket (format-response-json error-response))))))
                     ;; Handle non-promise responses
                     (do
                       (do-log options :debug "[Server] Sending response:" (pr-str response))
                       (.write socket (format-response-json response))))))))
           (.on socket "error"
                (fn [err]
                  (do-log options :error "[Server] Socket error:" socket-id peer-label err)))))))

(defn- start-socket-server!+ [{:keys [port] :as options}]
  (p/create
   (fn [resolve-fn reject]
     (try
       (let [server (.createServer
                     net
                     (fn [^js socket]
                       (setup-socket-handlers! options socket)))
             listen-port (or port 0)]
         (.listen server listen-port
                  (fn []
                    (let [address (.address server)
                          assigned-port (.-port address)
                          falling-back? (and (not= 0 listen-port)
                                             (not= listen-port assigned-port))]
                      (do-log options :info "[Server] Socket server listening on port" assigned-port)
                      (resolve-fn (merge {:server/instance server :server/port assigned-port}
                                         (when falling-back?
                                           {:server/port-note (str "NOTE: Port " port " was already in use.")}))))))
         (.on server "error"
              (fn [err]
                (if (and (= (.-code err) "EADDRINUSE")
                         (not= listen-port 0))
                  (do
                    (do-log options :warn (str "[Server] Port " listen-port " already in use, falling back to an available port"))
                    ;; Try again with port 0 (available port)
                    (.listen server 0))
                  (do
                    (do-log options :error "[Server] Server creation error:" err)
                    (reject err))))))
       (catch js/Error e
         (do-log options :error "[Server] Error creating server:" (.-message e))
         (reject e))))))

(defn send-notification-params [{:keys [active-sockets] :as options} notification]
  (let [sockets @active-sockets]
    (when (seq sockets)
      (doseq [socket sockets]
        (try
          (.write socket (str (js/JSON.stringify (clj->js notification)) "\n"))
          (catch js/Error e
            (do-log options :error "[Server] Error sending notification:" (.-message e))))))))

(defn start-server!+
  "Creates a socket server and writes the port to a file if port-file-uri is provided.
   Returns a promise that resolves to a map with server info when the MCP server starts successfully."
  [{:keys [port-file-uri] :as options}]
  (let [runtime-options (merge options {:active-sockets (atom #{})
                                        :socket-id-counter (atom 0)})]
    (p/let [server-info+ (start-socket-server!+ runtime-options)
            port (:server/port server-info+)]
      (if port-file-uri
        (let [port-file-dir (vscode/Uri.joinPath port-file-uri "..")]
          (p/do!
           (vscode/workspace.fs.createDirectory port-file-dir)
           (.writeFile vscode/workspace.fs port-file-uri (js/Buffer.from (str port)))
           (do-log runtime-options :info "Wrote port file:" (.-fsPath port-file-uri))
           (merge server-info+ runtime-options {:server/port-file-uri port-file-uri})))
        (merge server-info+ runtime-options)))))

(defn- close-server!+ [{:keys [active-sockets server/instance] :as options}]
  (do-log options :info "Stopping socket server...")
  (-> (p/create
       (fn [resolve-fn reject]
         (when (seq @active-sockets)
           (do-log options :info "Closing all active socket connections (" (count @active-sockets) ")...")
           (doseq [^js socket @active-sockets]
             (try
               (.end socket)
               (.destroy socket)
               (catch js/Error e
                 (do-log options :warn "[Server] Error closing socket:" (.-message e))))))
         (reset! active-sockets #{})
         (.close instance
                 (fn [err]
                   (if err
                     (do
                       (do-log options :error "[Server] Error stopping socket server:" err)
                       (reject err))
                     (do
                       (do-log options :info "Socket server stopped.")
                       (resolve-fn true)))))))
      (p/catch (fn [err2]
                 (do-log options :error "[Server] Error stopping socket server:" err2)))))

(defn stop-server!+
  "Stops the MCP server and removes the port file.
   Returns a promise that resolves to a boolean indicating success."
  [{:keys [server/instance server/port-file-uri] :as options}]
  (if-not instance
    (do
      (do-log options :info "No server instance provided to stop.")
      (p/resolved false))
    (-> (close-server!+ options)
        (p/then (fn [_]
                  (when port-file-uri
                    (delete-port-file!+ options port-file-uri))))
        (p/then (fn [_] true))
        (p/catch (fn [err]
                   (do-log options :error "[Server] Error during server shutdown:" err)
                   false)))))
